// To test: gradlew clean test
// To build: gradlew buildPlugin
// Check all tasks: gradlew tasks

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.6.5'
}

publishPlugin {
    token = System.getenv("ORG_GRADLE_PROJECT_intellijPublishToken")
}

group 'robotframework.intellij'
version '0.42.0'

task finishBuild(type: Exec) {
    def finishArgs = ['python', "finish_build.py"]
    commandLine finishArgs
}

buildPlugin.finalizedBy finishBuild

repositories {
    mavenCentral()

    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

allprojects {
    apply plugin: 'java'
    sourceCompatibility = 11
    targetCompatibility = 11
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    compile group: 'org.eclipse.lsp4j', name: 'org.eclipse.lsp4j', version: '0.12.0'
    compile group: 'org.eclipse.lsp4j', name: 'org.eclipse.lsp4j.debug', version: '0.12.0'
    compile 'org.markdownj:markdownj-core:0.4'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version '2020.3'
}

patchPluginXml {
    changeNotes """
    <p>
    Release 0.42.0
    </p>
    <ul>
  
    <li>Semantic highlighting supports Gherkin style (Given, Then, ...). (patch by @weltings)</li>
    <li>Semantic highlighting only highlights names up to a dot if a related import/resource is found to avoid conflict with Keywords with a dot in the name.(patch by @weltings)</li>
    <li>Semantic highlighting available for variables in keyword calls.</li>
    <li>Semantic highlighting for WHILE limit= and EXCEPT type=.</li>
    <li>Report unresolved variables when resolving a resource/import.</li>
    <li>Performance improvement: Don't index everything in AST when only items from the section (LibraryImport, ResourceImport, ...) are requested.</li>
    <li>Performance improvement: Caching of dependencies.</li>
    <li>Run Keyword If is now properly handled across all features in the language server.</li>
    <li>None is no longer reported as undefined keyword when used as keyword call.</li>
    <li>Misleading message saying that RF is old when it's not installed is no longer shown.</li>

    <br>
    Current feature set:<br>
    <ul>
    <li>Settings page for the language server</li>
    <li>Code completion</li>
    <li>Code analysis</li>
    <li>Go to definition</li>
    <li>Hover</li>
    <li>Code folding</li>
    <li>Browse Keywords (symbols)</li>
    <li>Syntax highlighting (with <strong>semanticTokens/full</strong> request)</li>
    <li>Debugger:</li>
    <ul>    
      <li>Add line breakpoints in <strong>.robot</strong> or <strong>.py</strong> files.</li>
      <li>Break when an error or failure is logged with the following environment variables:</li>
      <ul>
        <li><strong>RFLS_BREAK_ON_FAILURE=1</strong></li>
        <li><strong>RFLS_BREAK_ON_ERROR=1</strong></li>
      </ul>
      <li>Evaluate keywords.</li>
      <li>Pause at breakpoints to inspect the stack and see variables.</li>
      <li>Step in.</li>
      <li>Step over.</li>
      <li>Step return.</li>
      <li>Continue.</li>
    </ul>    

    </ul>
    <br>
    <strong>
    Important: The Intellij Language Server integration is currently in alpha for early access.<br>
    Please report any issues found during testing at:<br>
    <br>
    <a href="https://github.com/robocorp/robotframework-lsp/issues">https://github.com/robocorp/robotframework-lsp/issues</a>.
    </strong>
      """

    // See https://plugins.jetbrains.com/docs/intellij/gradle-guide.html?from=jetbrains.org#common-gradle-plugin-configurations-for-development
    // See https://plugins.jetbrains.com/docs/intellij/build-number-ranges.html?from=jetbrains.org#intellij-platform-based-products-of-recent-ide-versions

    sinceBuild '203'
    untilBuild '213.*'
}

test {
    useJUnit()

    systemProperty 'NO_FS_ROOTS_ACCESS_CHECK', true
    systemProperty 'idea.ProcessCanceledException', 'disabled'

    testLogging {
        // Show that tests are run in the command-line output
        events 'started', 'passed'
        showStandardStreams true
    }
}