// To test: gradlew clean test
// To build: gradlew buildPlugin
// Check all tasks: gradlew tasks

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.9.0'
}

publishPlugin {
    token = System.getenv("ORG_GRADLE_PROJECT_intellijPublishToken")
}

group 'robotframework.intellij'
version '1.5.0'

task finishBuild(type: Exec) {
    def finishArgs = ['python', "finish_build.py"]
    commandLine finishArgs
}

buildPlugin.finalizedBy finishBuild

repositories {
    mavenCentral()

    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

java {
    sourceCompatibility = 11
    targetCompatibility = 11
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    implementation group: 'org.eclipse.lsp4j', name: 'org.eclipse.lsp4j', version: '0.12.0'
    implementation group: 'org.eclipse.lsp4j', name: 'org.eclipse.lsp4j.debug', version: '0.12.0'
    implementation group: 'org.commonmark', name: 'commonmark', version: '0.19.0'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = '2021.3'
}

patchPluginXml {
    changeNotes = """
    <p>
    <strong>Release 1.5.0</strong>
    </p>
    
    <ul>
    <li><strong>Bugfixes</strong></li>
    <ul>
    <li>Fixed caching issue where adding a file would not invalidate an internal cache which could make resolved imports appear as unresolved.</li>
    </ul>
    
    
    
    <strong>
    See changelog for all versions at:<br>
    <br>
    <a href="https://github.com/robocorp/robotframework-lsp/blob/master/robotframework-intellij/docs/changelog.md">Changelog at GitHub</a>.
    </strong>


    <br>
    <strong>Current feature set:</strong><br>
    <ul>
        <li>Settings page for the language server</li>
        <li>Code completion</li>
        <li>Code analysis</li>
        <li>Go to definition</li>
        <li>Hover</li>
        <li>Code folding</li>
        <li>Browse Keywords (symbols)</li>
        <li>Syntax highlighting (with <strong>semanticTokens/full</strong> request)</li>
        <li>Debugger:</li>
        <ul>    
          <li>Add line breakpoints in <strong>.robot</strong> or <strong>.py</strong> files.</li>
          <li>Break when an error or failure is logged with the following environment variables:</li>
          <ul>
            <li><strong>RFLS_BREAK_ON_FAILURE=1</strong></li>
            <li><strong>RFLS_BREAK_ON_ERROR=1</strong></li>
          </ul>
          <li>Evaluate keywords.</li>
          <li>Pause at breakpoints to inspect the stack and see variables.</li>
          <li>Step in.</li>
          <li>Step over.</li>
          <li>Step return.</li>
          <li>Continue.</li>
        </ul>    
    </ul>
    
    </ul>
    <br>
    <strong>
    Issues may be reported at:<br>
    <br>
    <a href="https://github.com/robocorp/robotframework-lsp/issues">https://github.com/robocorp/robotframework-lsp/issues</a>.
    </strong>
      """

    // See https://plugins.jetbrains.com/docs/intellij/gradle-guide.html?from=jetbrains.org#common-gradle-plugin-configurations-for-development
    // See https://plugins.jetbrains.com/docs/intellij/build-number-ranges.html?from=jetbrains.org#intellij-platform-based-products-of-recent-ide-versions

    sinceBuild = '203'
    untilBuild = '223.*'
}

test {
    useJUnit()

    systemProperty 'NO_FS_ROOTS_ACCESS_CHECK', true
    systemProperty 'idea.ProcessCanceledException', 'disabled'

    testLogging {
        // Show that tests are run in the command-line output
        events 'started', 'passed'
        showStandardStreams true
    }
}